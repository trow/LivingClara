BACKUP ~LivingClara/backup~

AUTHOR ~MephistoSatanDevil Authorization from BCaesar and Ratatoskr (Briana)~

VERSION ~Version 0.999~

README ~LivingClara/translations/%LANGUAGE%/readme.txt~ ~LivingClara/readme.md~

AUTO_TRA ~LivingClara/translations/%s~

ALWAYS
  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN
    ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter //store adjusted chapter numbers in %bg_chapter_#% variables for use in .baf/.d scripts (e.g., %bg_chapter_1% will evaluate to 13 for EET and to 1 otherwise). This only works if you add the EVALUATE BUFFER code when compiling files with chapter triggers. See below.
    END
  END
END


LANGUAGE ~English~ ~english~
		 ~LivingClara/translations/english/setup.tra~
LANGUAGE ~Russian~ ~russian~
		 ~LivingClara/translations/russian/setup.tra~
LANGUAGE ~Simplified Chinese~ ~schinese~
		 ~LivingClara/translations/schinese/setup.tra~
		 ~LivingClara/translations/schinese/prompts.tra~


BEGIN @1 /*~All Things Mazzy~*/        //Any text written after Begin will appear as XXX in the Weidu install: Install XXX?

// Adds CD_STATE_NOTVALID state from CamDawg. May be unnecessary when coding for EE.
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~	

//Code to give Wilson proper dialogue files, courtesy of LavaDelVortel so our mods don't conflict
ACTION_IF FILE_EXISTS_IN_GAME ~wilson.dlg~ THEN BEGIN
 PRINT @137 /*Wilson detected - Wilson interdia.2da append - set Wilsons's banter file (BWILSON & BWILSO25)*/ //PRINT ~~ lets you add visible text to the Weidu setup. Can be tra-ified just like anything else.
    APPEND ~interdia.2da~ ~WILSON BWILSON BWILSO25~
    UNLESS ~WILSON~
	
	COPY_EXISTING ~pdialog.2da~ ~override~
  REPLACE_TEXTUALLY ~^[ %TAB%]*WILSON[ %TAB%].+$~
                    ~WILSON                 WILSONP                WILSONJ                WILSOND                WILSO25P               WILSO25J               WILSO25D               WILSON25~
BUT_ONLY_IF_IT_CHANGES //This stops Weidu from repeating the action if another mod already installed Wilson's files

//Dialogue Files
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSON.dlg~) THEN BEGIN
   COMPILE ~LivingClara/dialogues/BWILSONFix.d~
  END
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSO25.dlg~) THEN BEGIN
   COMPILE ~LivingClara/dialogues/BWILSO25Fix.d~
  END
    ACTION_IF NOT (FILE_EXISTS_IN_GAME ~WILSO25J.dlg~) THEN BEGIN
   COMPILE ~LivingClara/dialogues/WILSO25JFix.d~
  END
END

//TOB Script
ACTION_IF NOT (FILE_EXISTS_IN_GAME ~Wilson25.bcs~) THEN BEGIN
  PRINT @138 /*Wilson has no ToB override script? That must be changed!*/
  COMPILE ~LivingClara/scripts/wilson25.baf~
END

COPY_EXISTING ~Wilson13.cre~ ~override~ // Wilson version summoned by Fate Spirit in new ToB game (this version will NOT be summoned in EET if Wilson was with the party in SoA)
  WRITE_EVALUATED_ASCII DIALOG          ~WILSON25~ #8
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~WILSON25~ #8
  ADD_CRE_ITEM ~L#wilroc~ #1 #0 #0 ~identified~ ~qitem~ // missed quest reward from SoA
  
  
//This code adds scriptnames to several NPCs that lack them in the normal game. This can cause issues with any other mods that try to do the same, so use sparingly. These scriptnames will only appear on newly created characters, so they will not appear if you install the mod mid-game.
    // ----- 为缺少脚本名的 NPC 添加脚本名 -----
    // 这部分代码为游戏中一些默认没有死亡变量/脚本名 (DEATHVAR/Scriptname) 的 CRE 文件添加一个。
    // 这对于通过脚本精确控制这些 NPC 是必要的，但可能与其他修改相同 NPC 的 Mod 冲突。
    // 这些脚本名只对新创建的角色生效，对安装 Mod 时已存在的角色无效。

    // 为 "Rose Bouquet" (桥区妓女) 添加脚本名 "murdgirl"。
 COPY_EXISTING ~MURDGIRL.cre~ ~override~ //Rose Bouquet (Bridge Prostitute)
  WRITE_EVALUATED_ASCII DEATHVAR        ~murdgirl~ #32
  BUT_ONLY_IF_IT_CHANGES 
  
    // 为 "Gereth" (冒险者市场法师) 添加脚本名 "Gereth"。
 COPY_EXISTING ~GERETH.cre~ ~override~ //Gereth (Mage in Adventurer's Mart)
  WRITE_EVALUATED_ASCII DEATHVAR        ~Gereth~ #32
  BUT_ONLY_IF_IT_CHANGES 

    // 为 "Bertrand the Companion" 添加脚本名 "Bertrand"。  
COPY_EXISTING ~BERTRAND.cre~ ~override~ //Bertrand the Companion
  WRITE_EVALUATED_ASCII DEATHVAR        ~Bertrand~ #32
  BUT_ONLY_IF_IT_CHANGES 
 
    // 为 "Courtesan" (BPROST2.cre) 添加脚本名 "bprost2"。
COPY_EXISTING ~BPROST2.cre~ ~override~ //Courtesan
  WRITE_EVALUATED_ASCII DEATHVAR        ~bprost2~ #32
  BUT_ONLY_IF_IT_CHANGES 

    // 为 "Harlot" (DHARLOT1.cre) 添加脚本名 "dharlot1"。
COPY_EXISTING ~DHARLOT1.cre~ ~override~ //Harlot
  WRITE_EVALUATED_ASCII DEATHVAR        ~dharlot1~ #32
  BUT_ONLY_IF_IT_CHANGES   
		 
		 
PRINT @139 /*Clara not detected. Clara is hiding. Casting True Sight and adding Clara's content.*/

// 复制 Clara 相关的所有预设文件（如物品、脚本等，不需要文本修改的）到游戏的 override 目录。

// ----- 安装 Clara 在 SOA (Shadows of Amn) 阶段的 CRE 文件 -----
// 复制并重命名 Clara 的主要角色文件 (YF_clara.cre)。

COPY ~LivingClara/Characters/YF_clara.cre~ ~override~
    // 设置角色在游戏中的显示名称 (Name1) 和鼠标悬停名称 (Name2)。
	SAY NAME1 @2 /*Clara*/
	SAY NAME2 @2 /*Clara*/
	// 设置角色的背景故事 (Biography)。
	SAY BIO @3 /*Clara is hesitant to tell you much about her past, but she will share some facts when pressed. The young woman was raised on a farm and came to Athkatla several years ago, hoping to find work as an actress. Her efforts were unsuccessful so she learned the basics of thievery from an old Shadow Thief in order to scrape by. Although only a passable thief, Clara earned enough to pay the guild regular dues for permission to work in the city until she fell into Hexxat's thrall.*/
    // 设置各种游戏状态下的语音文本。
	SAY MORALE @4 /*I don't wanna die again!*/
	SAY LEADER @5 /*You can trust me to make us rich.*/
	SAY TIRED @6 /*Can we please stop to rest? I need my beauty sleep.*/
	SAY BORED @7 /*How long must I wait before we do something interesting?*/
	SAY BATTLE_CRY1 @8 /*Feel my dagger in your back!*/
	SAY BATTLE_CRY2 @9 /*I will fight you... from very far away.*/
	SAY BATTLE_CRY3 @10 /*Now you see me. Now you don't. Now you die!*/
	SAY BATTLE_CRY4 @11 /*Look over there! (And now I strike.)*/
	SAY BATTLE_CRY5 @12 /*This lady will kill you for you insolence!*/
	SAY DAMAGE @13 /*That hurt! How dare you touch a lady?*/
	SAY DYING @14 /*Oh, no. Time for my death scene.*/
	SAY HURT @15 /*I am injured. I must hide.*/
	SAY AREA_FOREST @16 /*I don't like the woods. There are so many bugs.*/
	SAY AREA_CITY @17 /*Men to flatter, people to swindle. It's good to be back where I belong.*/
	SAY AREA_DUNGEON @18 /*Caroline Aurora Lucia Rosena Silvershield is too good for this place.*/
	SAY AREA_DAY @19 /*The sun is nice but I prefer the dark. That is where I truly shine.*/
	SAY AREA_NIGHT @20 /*This is the time for secrets. The night hides everything.*/
    // 设置玩家在队伍中与 Clara 互动时的通用选择语音。
	SAY SELECT_COMMON1 @21 /*What do you want?*/
	SAY SELECT_COMMON2 @22 /*Do you need me?*/
	SAY SELECT_COMMON3 @23 /*How can this noble lady help?*/
	SAY SELECT_COMMON4 @24 /*Yes?*/
	SAY SELECT_COMMON5 @25 /*I'm listening.*/
	SAY SELECT_COMMON6 @26 /*What is it now? I'm busy.*/
    // 设置玩家指派 Clara 执行任务时的选择语音。
	SAY SELECT_ACTION1 @27 /*I'll do my best.*/
	SAY SELECT_ACTION2 @28 /*Happy to help.*/
	SAY SELECT_ACTION3 @29 /*It will be done.*/
	SAY SELECT_ACTION4 @30 /*I'm not meant for work like this.*/
	SAY SELECT_ACTION5 @31 /*Do I have to?*/
	SAY SELECT_ACTION6 @32 /*Fine. I'll do as you command.*/
	SAY SELECT_ACTION7 @33 /*As you wish. (Where's a big strong man to carry things?)*/
    // 设置 Clara 的稀有互动语音。
	SAY SELECT_RARE1 @34 /*Uggh, you're so demanding.*/
	SAY SELECT_RARE2 @35 /*Go away. I'm flirting.*/
    // 设置战斗相关语音。
	SAY CRITICAL_HIT @36 /*Right in the heart!*/
	SAY CRITICAL_MISS @37 /*Oops. I hope no one saw that.*/
	SAY TARGET_IMMUNE @38 /*My weapon isn't working. Usually things bleed.*/
    // 设置物品和状态相关语音。
	SAY INVENTORY_FULL @39 /*I am not a beast of burden. I can't carry anymore.*/
	SAY HAPPY @40 /*This is the most wonderful day. I'm rich and I'm pretty; what more could a girl want?*/
	SAY UNHAPPY_ANNOYED @41 /*I don't want to do this.*/
	SAY UNHAPPY_SERIOUS @42 /*Uggh. I'd rather be working for the Shadow Thieves again.*/
	SAY SET_A_TRAP @43 /*That was lucky. I thought it might explode.*/
	SAY HIDDEN_IN_SHADOWS @44 /*I shall slip away unseen.*/
	SAY PICKED_POCKET @45 /*Your coin belongs to me.*/
	SAY REACT_TO_DIE_GENERAL @46 /*I think I'm going to be sick.*/
	
// ----- 安装 Clara 在 TOB (Throne of Bhaal) 阶段的 CRE 文件 -----
// 复制并重命名 Clara 在 TOB 阶段的角色文件 (YF_cla25.cre)。
COPY ~LivingClara/Characters/YF_cla25.cre~ ~override~
    // 为 TOB 阶段的 Clara 文件设置相同的名称、背景和语音，确保角色一致性。
	SAY NAME1 @2 /*Clara*/
	SAY NAME2 @2 /*Clara*/
	SAY BIO @3 /*Clara is hesitant to tell you much about her past, but she will share some facts when pressed. The young woman was raised on a farm and came to Athkatla several years ago, hoping to find work as an actress. Her efforts were unsuccessful so she learned the basics of thievery from an old Shadow Thief in order to scrape by. Although only a passable thief, Clara earned enough to pay the guild regular dues for permission to work in the city until she fell into Hexxat's thrall.*/	
	SAY MORALE @4 /*I don't wanna die again!*/
	SAY LEADER @5 /*You can trust me to make us rich.*/
	SAY TIRED @6 /*Can we please stop to rest? I need my beauty sleep.*/
	SAY BORED @7 /*How long must I wait before we do something interesting?*/
	SAY BATTLE_CRY1 @8 /*Feel my dagger in your back!*/
	SAY BATTLE_CRY2 @9 /*I will fight you... from very far away.*/
	SAY BATTLE_CRY3 @10 /*Now you see me. Now you don't. Now you die!*/
	SAY BATTLE_CRY4 @11 /*Look over there! (And now I strike.)*/
	SAY BATTLE_CRY5 @12 /*This lady will kill you for you insolence!*/
	SAY DAMAGE @13 /*That hurt! How dare you touch a lady?*/
	SAY DYING @14 /*Oh, no. Time for my death scene.*/
	SAY HURT @15 /*I am injured. I must hide.*/
	SAY AREA_FOREST @16 /*I don't like the woods. There are so many bugs.*/
	SAY AREA_CITY @17 /*Men to flatter, people to swindle. It's good to be back where I belong.*/
	SAY AREA_DUNGEON @18 /*Caroline Aurora Lucia Rosena Silvershield is too good for this place.*/
	SAY AREA_DAY @19 /*The sun is nice but I prefer the dark. That is where I truly shine.*/
	SAY AREA_NIGHT @20 /*This is the time for secrets. The night hides everything.*/
	SAY SELECT_COMMON1 @21 /*What do you want?*/
	SAY SELECT_COMMON2 @22 /*Do you need me?*/
	SAY SELECT_COMMON3 @23 /*How can this noble lady help?*/
	SAY SELECT_COMMON4 @24 /*Yes?*/
	SAY SELECT_COMMON5 @25 /*I'm listening.*/
	SAY SELECT_COMMON6 @26 /*What is it now? I'm busy.*/
	SAY SELECT_ACTION1 @27 /*I'll do my best.*/
	SAY SELECT_ACTION2 @28 /*Happy to help.*/
	SAY SELECT_ACTION3 @29 /*It will be done.*/
	SAY SELECT_ACTION4 @30 /*I'm not meant for work like this.*/
	SAY SELECT_ACTION5 @31 /*Do I have to?*/
	SAY SELECT_ACTION6 @32 /*Fine. I'll do as you command.*/
	SAY SELECT_ACTION7 @33 /*As you wish. (Where's a big strong man to carry things?)*/
	SAY SELECT_RARE1 @34 /*Uggh, you're so demanding.*/
	SAY SELECT_RARE2 @35 /*Go away. I'm flirting.*/
	SAY CRITICAL_HIT @36 /*Right in the heart!*/
	SAY CRITICAL_MISS @37 /*Oops. I hope no one saw that.*/
	SAY TARGET_IMMUNE @38 /*My weapon isn't working. Usually things bleed.*/
	SAY INVENTORY_FULL @39 /*I am not a beast of burden. I can't carry anymore.*/
	SAY HAPPY @40 /*This is the most wonderful day. I'm rich and I'm pretty; what more could a girl want?*/
	SAY UNHAPPY_ANNOYED @41 /*I don't want to do this.*/
	SAY UNHAPPY_SERIOUS @42 /*Uggh. I'd rather be working for the Shadow Thieves again.*/
	SAY SET_A_TRAP @43 /*That was lucky. I thought it might explode.*/
	SAY HIDDEN_IN_SHADOWS @44 /*I shall slip away unseen.*/
	SAY PICKED_POCKET @45 /*Your coin belongs to me.*/
	SAY REACT_TO_DIE_GENERAL @46 /*I think I'm going to be sick.*/

// ========== 模块 2: Living Clara NPC - Clara 专属新物品和法术 ==========
// 在安装日志中打印一条信息，表明正在添加新物品和法术。	
PRINT @140 /*Adding new items and spells.*/

// ----- Clara 的新物品和法术 -----
// --- 物品: Clara's Jansen Spectroscopes (YF_SCOPE.itm) ---
COPY ~LivingClara/Items/YF_SCOPE.itm~ ~override~ //Items that require additional text/script changes must be copied individually to avoid messing up the game.
    // 设置物品的名称和描述。
SAY NAME1 @70 /*Clara's Jansen Spectroscopes*/
SAY NAME2 @70 /*Clara's Jansen Spectroscopes*/
SAY DESC @71
    // 使用 LPF (Library Procedure Function) ADD_ITEM_EQEFFECT 来添加一个物品效果，
    // 此处用于设置物品的可用性标志（Usability Flag），限制只有特定角色可以使用。
LPF ADD_ITEM_EQEFFECT   //this code sets usability flag with correct scriptname listed
    // 传递给 ADD_ITEM_EQEFFECT 函数的整数变量 (INT_VAR)。
INT_VAR
opcode = 319 // 效果操作码 319 对应 "Usability Constant (AND/OR)"。
target = 1 // 目标类型：自体 (Self)。
timing = 2 // 效果计时：即时 (Instant/Permanent)。
parameter2 = 11    // 参数 2：设置为 11，通常与特定的可用性标志位相关。
power = 1   // 权限：1 表示只有指定的资源可以使用（白名单）。
			// 0 表示除了指定的资源都不能使用。
        // 传递给 ADD_ITEM_EQEFFECT 函数的字符串变量 (STR_VAR)。
special = RESOLVE_STR_REF (@2)  // 特殊参数：这里引用 @2 ("Clara") 的字符串，作为显示在物品上的文本（通常是角色名）。Whatever is referenced here will be shown as text on the item itself.
STR_VAR
resource = YF_Clara// 指定允许使用的角色脚本名是 "YF_Clara"。
END// 结束 LPF 调用。
// --- 物品: Clara's Jansen Techno-Gloves (YF_Glove.itm) ---
// 复制需要修改文本/脚本的单个物品文件。
COPY ~LivingClara/Items/YF_Glove.itm~ ~override~
    // 设置物品的名称和描述。
SAY NAME1 @74 /*Clara's Jansen Techno-Gloves*/
SAY NAME2 @74 /*Clara's Jansen Techno-Gloves*/
SAY DESC @75
    // 同样使用 LPF 添加可用性限制效果，只允许 Clara 使用。
LPF ADD_ITEM_EQEFFECT //this code sets usability flag with correct scriptname listed
INT_VAR
opcode = 319
target = 1
timing = 2
parameter2 = 11
power = 1
special = RESOLVE_STR_REF (@2) // 引用 "Clara"
STR_VAR
resource = YF_Clara// 限制给 "YF_Clara"
END

// --- 物品: Yoshimo's Katana +3 (YF_Kata.itm) ---
// 复制物品文件。
COPY ~LivingClara/Items/YF_Kata.itm~ ~override~
    // 设置名称和描述。
SAY NAME1 @76 /*Yoshimo's Katana +3*/
SAY NAME2 @76 /*Yoshimo's Katana +3*/
SAY DESC @77
    // 添加可用性限制效果，只允许 Clara 使用。
LPF ADD_ITEM_EQEFFECT
INT_VAR
opcode = 319
target = 1
timing = 2
parameter2 = 11
power = 1
special = RESOLVE_STR_REF (@2)// 引用 "Clara"
STR_VAR
resource = YF_Clara// 限制给 "YF_Clara"
END

// --- 物品: Delryn Family Ring (YF_Rng45.itm) ---
// 复制物品文件。
COPY ~LivingClara/Items/YF_Rng45.itm~ ~override~
    // 设置名称和描述。
SAY NAME1 @96 /*Delryn Family Ring*/
SAY NAME2 @96 /*Delryn Family Ring*/
SAY DESC @97
    // 添加可用性限制效果，只允许 Clara 使用。
    // 注释提到其他效果已在 Near Infinity 中创建，这个是在 setup 中添加的文本相关效果。
LPF ADD_ITEM_EQEFFECT //Other effects can be added here. We did most in Near Infinity when creating the item. This effect is in setup because it involves overwriting game text.
INT_VAR
opcode = 319
target = 1
timing = 2
parameter2 = 11
power = 1
special = RESOLVE_STR_REF (@2) // 引用 "Clara"
STR_VAR
resource = YF_Clara// 限制给 "YF_Clara"
END

COPY ~LivingClara/Items/YF_BELTC.itm~ ~override~ //Clara only version (actually made to work with a script. The gender effect tends to crash SOA randomly)
SAY NAME1 @680 /*Girdle*/
SAY NAME2 @68 /*Girdle of Femininity/Masculinity*/
SAY DESC @690 /*Similar to the rare yet oft-discussed cursed Girdle of Masculinity/Femininity, a magical item that changes the wearer's gender, this particular girdle was specifically enchanted to only work on Clara as a joke.*/
    // 注意：此物品虽然添加了效果，但注释说明其功能主要由脚本实现，
    // 并且直接的性别转换效果在 SOA 中可能不稳定。
	
// --- 物品: Girdle of Femininity/Masculinity - Edwin 专用版 (YF_BELTE.itm) ---
// 复制 Edwin 专用版本的腰带。
COPY ~LivingClara/Items/YF_BELTE.itm~ ~override~ //Edwin only version
    // 设置名称和描述。
SAY NAME1 @680 /*Girdle*/
SAY NAME2 @68 /*Girdle of Femininity/Masculinity*/
SAY DESC @691 /*Similar to the rare yet oft-discussed cursed Girdle of Masculinity/Femininity, a magical item that changes the wearer's gender, this particular girdle was enchanted to be only usuable by Edwin.*/
    // 添加可用性限制效果，只允许 Edwin 使用。
LPF ADD_ITEM_EQEFFECT
INT_VAR
opcode = 319
target = 1
timing = 2
parameter2 = 11
power = 1
special = RESOLVE_STR_REF (@681) // 引用 "Edwin"
STR_VAR
resource = Edwin// 限制给 "Edwin"
END 

// 单独复制需要修改文本或效果的物品文件。
// 复制并修改 YF_BELT2.itm (Girdle of Femininity/Masculinity)。
COPY ~LivingClara/Items/YF_BELT2.itm~ ~override~ //If you do have to change text/add effects, you need to copy the item individually.
    // 设置物品的名称和描述。
SAY NAME1 @680 /*Girdle*/
SAY NAME2 @68 /*Girdle of Femininity/Masculinity*/
SAY DESC @69 /*Similar to the rare yet oft-discussed cursed Girdle of Masculinity/Femininity, a magical item that changes the wearer's gender, this version is not cursed and was likely created simply for entertainment. The magic of the belt only affects halflings.*/

//If you don't have to change text, you can just copy files en masse
//New Items & Spells for Clara

COPY ~LivingClara/Items/YF_cask2.ITM~ ~override~
COPY ~LivingClara/Items/YF_BELT0.ITM~ ~override~

COPY ~LivingClara/Spells/YF_BKSTB.SPL~ ~override~
COPY ~LivingClara/Spells/YF_FTRAP.SPL~ ~override~
COPY ~LivingClara/Spells/YF_HIDE.SPL~ ~override~
COPY ~LivingClara/Spells/YF_intox.SPL~ ~override~
COPY ~LivingClara/Spells/YF_KATA.SPL~ ~override~
COPY ~LivingClara/Spells/YF_LOCKS.SPL~ ~override~
COPY ~LivingClara/Spells/YF_LONG.SPL~ ~override~
COPY ~LivingClara/Spells/YF_MOVE.SPL~ ~override~
COPY ~LivingClara/Spells/YF_NOND.SPL~ ~override~
COPY ~LivingClara/Spells/YF_PICKP.SPL~ ~override~
COPY ~LivingClara/Spells/YF_REP.SPL~ ~override~
COPY ~LivingClara/Spells/YF_SBOW.SPL~ ~override~
COPY ~LivingClara/Spells/YF_SDO.SPL~ ~override~
COPY ~LivingClara/Spells/YF_SSP.SPL~ ~override~
COPY ~LivingClara/Spells/YF_STRAP.SPL~ ~override~
COPY ~LivingClara/Spells/YF_TIRED.SPL~ ~override~
COPY ~LivingClara/Spells/YF_TRAP.SPL~ ~override~

// --- 法术: Create Poison Arrows (YF_PoisA.spl) ---
// 复制 Clara 的新法术文件。
COPY ~LivingClara/Spells/YF_PoisA.spl~ ~override~ 
    // 设置法术的名称和未鉴定描述。
SAY NAME1 @72 /*Create Poison Arrows*/
SAY UNIDENTIFIED_DESC @73 /*This ability allows Clara to create 15 poisonous Arrows of Biting, adding them to her inventory. Handle with care!*/
// --- 法术: Set Fireball Trap (YF_FIRE.spl) ---
// 复制 Clara 的新法术文件。
COPY ~LivingClara/Spells/YF_FIRE.spl~ ~override~ 
    // 设置法术的名称和未鉴定描述。
SAY NAME1 @105 /*Set Fireball Trap*/
SAY UNIDENTIFIED_DESC @106 /*This ability allows Clara to create a Fireball trap that will stay in position until someone walks too close. Friend or foe, the Fireball will then explode for massive damage.*/

// ----- 复制并设置用于幽魂场景的新生物 (CRE) 文件 -----
// 复制 "Little Clara" 的幽魂生物文件，并设置其属性。
  COPY ~LivingClara/Characters/YF_LITCL.bmp~ ~override~
  COPY ~LivingClara/Characters/YF_LITCL.cre~ ~override~
      // 设置名称。
  SAY NAME1 @130 /*Little Clara*/
  SAY NAME2 @130
      // 设置对话文件、死亡变量和覆盖脚本。
  WRITE_EVALUATED_ASCII DIALOG          ~YF_LITCL~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~YF_LITCL~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~~       #8 
  
COPY ~LivingClara/Characters/YF_Hdead.cre~ ~override~

// ========== 模块 2: Living Clara NPC - Clara 核心脚本与对话安装 ==========
PRINT @141 /*Setting up Jan Jansen's Ball of Superdark Darkness.*/

// ----- 更新游戏核心的 2DA 文件以注册 Clara 的对话 -----
// 将 Clara 的条目添加到 pdialog.2da 文件中。
// pdialog.2da 定义了队伍中 NPC 的各种对话文件（如 Player-initiated, Journal, Dialogue 等）。
// 注释说明：这里只列出队伍相关的文件，顺序很重要：
// (override_script, p_dialog, j_dialog, d_dialog, tob_override, tob_p, tob_j, tob_d)
APPEND ~pdialog.2da~ //This is only for party files, so the default d files aren't here. Order matters (script, p, j, dscript, tobp, tobj, tobd, tobdscript)
~YF_Clara     YF_clarp     YF_clarj     YF_clard     YF_cl25p     YF_cl25j     YF_cl25d     YF_cla25~
UNLESS ~YF_Clara~

// 将 Clara 的条目添加到 interdia.2da 文件中。
// interdia.2da 定义了 NPC 之间的闲聊（互动对话）文件。
// 格式通常是：NPC脚本名 SoA闲聊文件 ToB闲聊文件
APPEND ~interdia.2da~
~YF_Clara     bYF_clar     bYF_cl25~
UNLESS ~YF_Clara~// 同样使用 UNLESS 防止重复添加。

// ----- 编译 Clara 的对话文件 -----
//EVALUATE_BUFFER is required on any file that uses the EET chapter code as a trigger. It can go on all files to be safe.
COMPILE EVALUATE_BUFFER ~LivingClara/Dialogues/bYF_cl25.d~
	~LivingClara/Dialogues/bYF_clar.d~
	~LivingClara/Dialogues/YF_cla25.d~
	~LivingClara/Dialogues/YF_cl25j.d~
	~LivingClara/Dialogues/YF_cl25p.d~
	~LivingClara/Dialogues/YF_clara.d~
	~LivingClara/Dialogues/YF_claraanomen.d~
	~LivingClara/Dialogues/YF_clarainterjections.d~
	~LivingClara/Dialogues/YF_clarj.d~
	~LivingClara/Dialogues/YF_ClaraMazzy.d~
	~LivingClara/Dialogues/YF_clarp.d~
	~LivingClara/Dialogues/YF_claraquest.d~
	~LivingClara/Dialogues/YF_hexxattalk.d~
	~LivingClara/Dialogues/YF_NeeraBook.d~
	~LivingClara/Dialogues/YF_ohhfakthieves.d~
	
//Dialogue Files- always installed (this code means whole folder compiled. You can also compile each dialogue file individually if you list the names)
// ----- 编译善良/邪恶路线共用的 SOA 对话文件 -----
// 编译 /soa 目录下的所有对话文件。
// EVALUATE_BUFFER 确保处理文件中的变量。
COMPILE EVALUATE_BUFFER ~LivingClara/dialogues/YF_anomenfixsoa.d~	
	~LivingClara/dialogues/YF_darksideanomensoa.d~
	~LivingClara/dialogues/YF_DSAnomenromancesoa.d~
PRINT @152 /*Searching for pure, chaste Imoen.
Error: Chaste Imoen not found.
Adding inappropriate sex-loving Imoen.
Sex-loving Imoen added.
Searching for halfling orgy.
No orgy found. Insufficient halflings.*/
// ----- 编译 TOB 阶段的善良/中立路线对话文件 -----
// 编译 /tob 目录下的所有对话文件。
COMPILE ~LivingClara/dialogues/YF_anomenfixtob.d~

// ----- 编译 Clara 的行为脚本 (BAF -> BCS) -----
// 编译 Clara 在 SOA 阶段的覆盖脚本 (YF_clara.baf -> YF_clara.bcs)。
COMPILE EVALUATE_BUFFER ~LivingClara/Scripts/YF_clara.baf~
// 编译 Clara 在 SOA 阶段的对话脚本 (YF_clard.baf -> YF_clard.bcs)。
COMPILE EVALUATE_BUFFER ~LivingClara/Scripts/YF_clard.baf~
// 编译 Clara 在 TOB 阶段的覆盖脚本 (YF_cla25.baf -> YF_cla25.bcs)。
COMPILE ~LivingClara/Scripts/YF_cla25.baf~
// 编译 Clara 在 TOB 阶段的对话脚本 (YF_cl25d.baf -> YF_cl25d.bcs)。
COMPILE ~LivingClara/Scripts/YF_cl25d.baf~
// 编译 Gereth 相关的脚本（用于 Clara 的第二个任务）。
COMPILE ~LivingClara/scripts/YF_Gereth.baf~ EVALUATE_BUFFER  //2nd Clara Quest

// ----- 扩展现有游戏脚本以整合 Clara 的剧情 -----
// 将 Clara 的获取脚本 (YF_gettingclara.baf) 添加到游戏主脚本 baldur.bcs 的顶部 (EXTEND_TOP)。
// 这样可以确保 Clara 相关的初始化或触发逻辑尽早执行。
// EVALUATE_BUFFER 确保脚本中的变量被正确处理。
// 注释警告：将代码放在脚本顶部可能很危险，因为持续触发的代码可能会阻止后续代码执行，需要仔细测试。
EXTEND_TOP ~baldur.bcs~ ~LivingClara/Scripts/YF_gettingclara.baf~ EVALUATE_BUFFER //Extend Top puts code at top of bcs file so it plays first. This can be dangerous since code that fires continuously will stop anything after it from triggering. TEST TEST TEST.

//前面的脚本拆成两部分，第一部分保留在全局脚本，第二部分检查物品的代码太多，召唤鸟YF_Hdead来承载by shohy
COMPILE ~LivingClara/Scripts/YF_Hdead.baf~ EVALUATE_BUFFER
COMPILE ~LivingClara/Dialogues/YF_Hdead.d~ EVALUATE_BUFFER


//给伯里奇添加一个木桩by shohy
COPY_EXISTING ~ohhburic.cre~ ~override~
	ADD_CRE_ITEM ~MISC6W~ #1 #0 #0 ~NONE~ ~INV7~
//给伯里奇对话里添加获得木桩
COPY_EXISTING ~ohhburic.dlg~ ~override~
    DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~GiveItem("OHHROD",Player1)~ ~GiveItem("OHHROD",Player1) GiveItem("MISC6W",Player1)~
    END
    BUT_ONLY_IF_IT_CHANGES

//给地图中的某个棺材里放一个复原术卷轴by MephistoSatanDevil
COPY_EXISTING ~oh7000.are~ ~override~
  LPF ADD_AREA_ITEM
  INT_VAR
    container_to_add_to = 4
	charges1 = 1
  STR_VAR
	item_to_add = ~RESTORE~
  END

// 将 Clara 与 "OHHFAK" NPC (可能是一个特定事件或 NPC) 的互动脚本 (YF_OHHFAKClara.baf)
// 添加到 OHHFAK.bcs 脚本的底部 (EXTEND_BOTTOM)。
// EVALUATE_BUFFER 同样用于处理脚本中的变量。
EXTEND_BOTTOM ~OHHFAK.bcs~ ~LivingClara/Scripts/YF_OHHFAKClara.baf~ EVALUATE_BUFFER

// ========== 模块 2: Living Clara NPC - EET 兼容性与跨 Mod 内容 ==========

// ----- EET (Enhanced Edition Trilogy) 相关代码与召唤脚本 -----
// 检查当前游戏是否为 EET。如果是，则执行 EET 特定的安装逻辑。
// EET related code & summoning script
ACTION_IF GAME_IS ~eet~ THEN BEGIN 
    // 包含 EET 提供的函数库文件，其中定义了用于处理 NPC 跨章节连续性的函数。
  INCLUDE ~EET/other/EET_functions.tph~ //declaration of external EET functions used below

    // 调用 EET_NPC_TRANSITION 函数 (LAF = Library Action Function)。
    // 这个函数用于自动处理 NPC 在 BG1、SOA、TOB 之间的过渡和召唤逻辑。
  LAF ~EET_NPC_TRANSITION~ //function used to automatically implement EET continuity system
    // 传递给 EET_NPC_TRANSITION 函数的整数变量 (INT_VAR)。
	INT_VAR
      type = 2  // 告诉函数这是一个纯 BG2 NPC（在 BG1 中没有内容）。//informs the function that this is BG2 NPC without BG1 content
	  default_ToB = 1 // 表示当玩家在 TOB 开始新游戏时，该 NPC 可以作为可召唤的 NPC 出现。//NPC available as summonable NPC when the game is started in ToB (= new ToB game)
	  clean_ToB = 1// 表示在 TOB 阶段使用一个“干净”的角色文件（无 SoA 状态）。
        // 传递给 EET_NPC_TRANSITION 函数的字符串变量 (STR_VAR)。
    STR_VAR
      dv = "YF_Clara"  // 指定 Clara 的死亡变量 (Death Variable / Script Name)。//Clara Death Variable (script name)
      override_SoA = "YF_Clara" // 指定在 SOA 阶段使用的覆盖脚本文件名 (不带 .bcs 扩展名)。//SoA Override script
      override_ToB = "YF_cla25" // 指定在 TOB 阶段使用的覆盖脚本文件名。//ToB Override script
      dialog_ToB = "YF_cla25"  // 指定在 TOB 阶段使用的主对话文件名。//ToB dialogue file
	  cre_ToB = "YF_cla25"// 指定在 TOB 阶段使用的角色文件名 (不带 .cre 扩展名)。
	  // 指定包含 Fate Spirit (命运之魂) 对话文本的翻译文件。
	  traFile = EVAL "LivingClara/translations/%LANGUAGE%/YF_cla25.tra"
      string = "@0" // 指定 traFile 中包含召唤台词的字符串引用。"@0" 通常是第一条文本。//"Bring me Clara, the human shadowdancer."
      stringPosDV = "Dorn" // 指定在 Fate Spirit 的召唤列表中，Clara 的条目应该插入在哪个 NPC (Dorn) 之前。//Clara should be appended right above Dorn
	  clean_ToB_DV = "YF_Clara" // 指定在 TOB 阶段用于初始化状态的死亡变量。
  END// 结束 LAF EET_NPC_TRANSITION 的调用。
END ELSE BEGIN // 如果游戏不是 EET，则执行以下不同的处理。//different Fate Spirit dialogue patching for any other platform than EET
    // 为非 EET 平台编译一个不同的 Fate Spirit 对话文件，使其能够召唤 Clara。
    // USING ~...~ 指定了编译时使用的翻译文件，确保召唤台词正确显示。
  COMPILE ~LivingClara/Dialogues/YF_FateSpirit.d~ USING ~LivingClara/translations/%LANGUAGE%/YF_cla25.tra~  //Fate Spirit Dialogue, same tra file as TOB dialogue with USING
END // 结束非 EET 平台的处理块。



// ========== 其他随机内容 - 日志条目 (Journal Entries) 和文件复制 ==========

// ----- 添加带有标题的 Enhanced Edition (EE) 风格日志条目 -----
// 打印信息，表明正在添加 EE 风格的日志标题。
//Journal Entries (This code gives quest journal entries the EE style titles)
PRINT @142 /*EE Journal Entries. Titles needed.*/
// 使用 ADD_JOURNAL 指令添加日志条目，并通过 TITLE 关键字指定标题。
// 格式: ADD_JOURNAL TITLE (@title_ref) @entry1_ref @entry2_ref ... USING ~path/to/translation.tra~
// 添加邪恶 Anomen (Darkside Anomen) 的日志。
ADD_JOURNAL TITLE (@1000) @1001 @1002 @1003 USING ~LivingClara/translations/%LANGUAGE%/YF_DSAnomenromancesoa.tra~ //Darkside Anomen 
// 添加 Clara 的第二个图书馆任务日志。
ADD_JOURNAL TITLE (@1005) @1006 @1007 @1008 @1009 @1010 @1011 @1012 USING ~LivingClara/translations/%LANGUAGE%/YF_claraquest.tra~ //2nd library quest

// ----- 复制主 Mod 的文件（法术、音乐等）-----
// 打印信息，表明正在添加新物品、能力等。
//Copied files for main mod - spells, music, etc. 
PRINT @143 /*Adding new items, new nonsense, new strange abilities...*/




//General Crossmod stuff:
// ========== 其他随机内容 - 跨 Mod (Crossmod) 内容 ==========

//Crossmod files for Clara only
// ----- Clara 的跨 Mod (Crossmod) 内容 -----
// 检查游戏中是否存在 Saradas NPC 的角色文件 (!sartob.cre)。
// 这通常意味着 Saradas NPC Mod 已安装。
ACTION_IF FILE_EXISTS_IN_GAME ~!sartob.cre~ THEN BEGIN //This code detects the Saradas NPC and adds his banters with Clara
    // 如果检测到 Saradas，则打印信息。
 PRINT @150 /*Saradas detected - adding crossmod banters*/
     // 编译 Clara 与 Saradas 之间的互动对话文件。
 COMPILE ~LivingClara/Dialogues/YF_SaradasBanters.d~ 
END ELSE BEGIN    // 如果未检测到 Saradas，则打印信息。
 PRINT @151 /*Saradas NPC not detected*/
END // 结束 Saradas 不存在的处理块。

// --- 跨 Mod 侏儒腰带代码 ("Alora in BG2" Mod) ---
// 检测 "Alora in BG2" Mod 是否安装 (通过检查 ALORA09.cre 文件)。
//Crossmod Halfling Belt Code ("Alora in BG2" mod)
ACTION_IF FILE_EXISTS_IN_GAME ~ALORA09.cre~ THEN BEGIN //This code detects the Alora NPC mod and adds our gender belt code to her script. Alora must be installed first.
    // 如果检测到，则打印信息。
	PRINT @144 /*'Alora in BG2' mod detected - adding crossmod Easter egg*/
    // 将腰带效果的脚本扩展添加到 Alora 的 SoA 脚本底部。
EXTEND_BOTTOM ~CMALORA.BCS~ ~LivingClara/scripts/YF_Alora.baf~ 
END ELSE BEGIN    // 如果未检测到，则打印信息。
 PRINT @145 /*'Alora in BG2' mod not detected*/
END

// 检测 "Alora in BG2" Mod 的 TOB 版本 (ALORA25.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~ALORA25.cre~ THEN BEGIN //Alora belt code
    // 将腰带效果的脚本扩展添加到 Alora 的 TOB 脚本底部。
EXTEND_BOTTOM ~CMALO25.BCS~ ~LivingClara/scripts/YF_Alora.baf~ 
END // 结束 Alora (TOB) 存在的处理块。

//Crossmod Halfling Belt Code (Smiling Imp's Alora)
// --- 跨 Mod 侏儒腰带代码 (Smiling Imp's "BG1 NPCs in SoA & ToB" Mod) ---
// 检测 Smiling Imp 的 Mod 中的 Alora 组件 (7XCRE69.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~7XCRE69.cre~ THEN BEGIN //This code detects the Alora component from "BG1 NPCs in Soa&ToB" mod and adds our gender belt code to her script. Alora must be installed first.
    // 如果检测到，则打印信息。
 PRINT @1440 /*'BG1 NPCs in SoA & ToB' Alora detected - adding crossmod Easter egg*/
    // 将特定的腰带效果脚本扩展添加到该版本 Alora 的 SoA 和 TOB 脚本底部。
EXTEND_BOTTOM ~7xAlor.BCS~ ~LivingClara/scripts/YF_AloraImp.baf~ 
EXTEND_BOTTOM ~7XAlor25.BCS~ ~LivingClara/scripts/YF_AloraImp.baf~ 
END ELSE BEGIN
    // 如果未检测到，则打印信息。
 PRINT @1450 /*'BG1 NPCs in SoA & ToB' Alora NPC not detected*/
END

// --- 跨 Mod 侏儒腰带代码 (Nephele NPC Mod) ---
// 检测 Nephele NPC Mod 是否安装 (lk#neph.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~lk#neph.cre~ THEN BEGIN //This code detects the Nephele NPC mod and adds our gender belt code to her script. Nephele must be installed first.
    // 如果检测到，则打印信息。
 PRINT @146 /*Nephele detected - adding crossmod Easter egg*/
    // 将腰带效果的脚本扩展添加到 Nephele 的 SoA 脚本底部。
EXTEND_BOTTOM ~LK#NEPH.BCS~ ~LivingClara/scripts/YF_Neph.baf~ 
END ELSE BEGIN
// 如果未检测到，则打印信息。
 PRINT @147 /*Nephele NPC not detected*/
END

// 检测 Nephele NPC Mod 的 TOB 版本 (lk#ne25.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~lk#ne25.cre~ THEN BEGIN //Nephele belt code
    // 将腰带效果的脚本扩展添加到 Nephele 的 TOB 脚本底部。
EXTEND_BOTTOM ~LK#NE25.BCS~ ~LivingClara/scripts/YF_Neph.baf~ 
END // 结束 Nephele (TOB) 存在的处理块。

// --- 跨 Mod 侏儒腰带代码 (Petsy NPC Mod) ---
// 检测 Petsy NPC Mod 是否安装 (L3Petsy.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~L3Petsy.cre~ THEN BEGIN //This code detects the Petsy NPC mod and adds our gender belt code to her script. Nephele must be installed first.
    // 如果检测到，则打印信息。
 PRINT @148 /*Petsy detected - adding crossmod Easter egg*/
    // 将腰带效果的脚本扩展添加到 Petsy 的 SoA 脚本底部。
EXTEND_BOTTOM ~L3Petsy.BCS~ ~LivingClara/scripts/YF_Petsy.baf~ 
END ELSE BEGIN
 PRINT @149 /*Petsy NPC not detected*/
// 结束 Petsy (SoA) 存在的处理块。
END
// 检测 Petsy NPC Mod 的 TOB 版本 (L3Pet25.cre)。
ACTION_IF FILE_EXISTS_IN_GAME ~L3Pet25.cre~ THEN BEGIN //Petsy belt code
    // 将腰带效果的脚本扩展添加到 Petsy 的 TOB 脚本底部。
EXTEND_BOTTOM ~L3Pet25.BCS~ ~LivingClara/scripts/YF_Petsy.baf~ 
 // 结束 Petsy (TOB) 存在的处理块。
END

COPY ~LivingClara/Audio/YF_Blank.mus~ ~override~
// This code is also used by BG2 Fixpack - it would help you to run custom music for your NPC from "a clean slate", installing "blank" music you could run it with. Not everyone installs the Fixpack, though, so here we go:
// Feel free to skip this code if you're not using any music in your mod, only your NPC's voice.
COPY_EXISTING ~songlist.2da~ ~override~
  SET_2DA_ENTRY 0 2 3 ~YF_Blank.mus~
// You will note there's a Blank.mus file in your Audio directory - it's needed exactly for that purpose. Delete it if you're not using any music in your mod.


PRINT @153 /*Searching for giant space hamster.
Giant space hamster found.
Miniaturizing initiated.
...
Miniaturization successful.*/

//Script Files - always installed
// ========== 模块 1: All Things Mazzy - 核心善良/中立路线脚本扩展 ==========

// ----- 扩展现有游戏脚本 (善良/中立路线) - 总是安装 -----
// 将善良/中立路线的通用脚本逻辑附加到游戏核心和 NPC 的脚本中。

// 将善良/中立路线的初始化逻辑添加到游戏主脚本 baldur.bcs 的顶部。
EXTEND_TOP ~baldur.bcs~ ~LivingClara/scripts/YF_baldur.baf~ EVALUATE_BUFFER
// 将 Anomen 的善良/中立路线逻辑添加到其脚本底部。
EXTEND_BOTTOM ~anomen.bcs~ ~LivingClara/scripts/YF_anomen.baf~ EVALUATE_BUFFER
// 将 Anomen 的 D 对话脚本的善良/中立路线逻辑添加到底部。
EXTEND_BOTTOM ~anomend.bcs~ ~LivingClara/scripts/YF_anomend.baf~ EVALUATE_BUFFER
// ----- 扩展特定区域脚本以支持 Mazzy 恋爱任务和其他剧情 -----
// 将 Clara 与 Anomen 相关剧情的脚本逻辑添加到 AR1000 的顶部。
EXTEND_TOP ~AR1000.bcs~ ~LivingClara/scripts/YF_AR1000.baf~ EVALUATE_BUFFER //Just for Clara-Anomen 
// 将 Clara 与 Anomen 相关剧情的脚本逻辑添加到 AR1001 的顶部。
EXTEND_TOP ~AR1001.bcs~ ~LivingClara/scripts/YF_AR1001.baf~ EVALUATE_BUFFER //Just for Clara-Anomen 
// 将 Clara 的第二个任务相关的脚本逻辑添加到 AR0702 的顶部。
EXTEND_TOP ~AR0702.bcs~ ~LivingClara/scripts/YF_AR0702.baf~ EVALUATE_BUFFER //2nd Clara Quest
// 将 Clara 的第二个任务相关的脚本逻辑添加到 AR1700 的顶部。
EXTEND_TOP ~AR1700.bcs~ ~LivingClara/scripts/YF_AR1700.baf~ EVALUATE_BUFFER //2nd Clara Quest

// 将 Terl NPC 的对话后备脚本逻辑添加到 terl.bcs 的底部。
EXTEND_TOP ~terl.bcs~ ~LivingClara/scripts/YF_Terl.baf~ EVALUATE_BUFFER //For all the Terl talks, mostly backup 

EXTEND_BOTTOM ~dornd.bcs~ ~LivingClara/scripts/YF_dorn.baf~ EVALUATE_BUFFER//??
EXTEND_BOTTOM ~dornd.bcs~ ~LivingClara/scripts/YF_dornd.baf~ EVALUATE_BUFFER//??
// 将 Hexxat 的善良/中立路线逻辑添加到其脚本底部。
EXTEND_BOTTOM ~hexxat.bcs~ ~LivingClara/scripts/YF_hexxat.baf~ EVALUATE_BUFFER
// 将 TOB 阶段 Anomen 的善良/中立路线逻辑添加到其脚本底部。
EXTEND_BOTTOM ~anom25.bcs~ ~LivingClara/scripts/YF_anomentob.baf~


// 将 TOB 阶段 Dorn 的善良/中立路线逻辑添加到其脚本底部。
EXTEND_BOTTOM ~dorn25.bcs~ ~LivingClara/scripts/YF_dorntob.baf~
// 将 TOB 阶段 Dorn 的 D 对话脚本的善良/中立路线逻辑添加到底部。
EXTEND_BOTTOM ~dorn25d.bcs~ ~LivingClara/scripts/YF_dorndtob.baf~
// 将 TOB 阶段 Hexxat 的善良/中立路线逻辑添加到其脚本底部。
EXTEND_BOTTOM ~hexxa25.bcs~ ~LivingClara/scripts/YF_hexxattob.baf~



//Wraith Scene Files
// ========== 模块 2: Living Clara - 幽魂场景相关生物文件 ==========


 
//CutScenes
// ========== 其他随机内容 - 过场动画 (CutScenes) 和结局 (Epilogues) ==========

// ----- 编译各种过场动画脚本 -----
// 编译 Clara-Anomen 幽魂剧情的开始脚本。
COMPILE ~LivingClara/Scripts/YF_LCla.baf~ //C-A Wraith Cutscene
// 编译 Clara-Anomen 幽魂剧情的结束脚本。
COMPILE ~LivingClara/Scripts/YF_LCla2.baf~ //End of Wraith Cutscene
// 编译 Clara 任务中与魔像互动的剧情脚本。
COMPILE ~LivingClara/scripts/YF_Gol.baf~ //Golem Scene Clara Quest

//Epilogues:
// ----- 结局处理 -----
// 在安装日志中打印一条关于创建结局的信息。
PRINT @154 /*Creating epilogues. Too many epilogues... Why are there so many options here?*/
// Changing AR6200 (existing Mazzy epilogue) - this is the normal way to do it. Dummied out since we made ours unstoppable
// --- 注释掉的旧版结局修改方法 ---
// 下面的代码块是被注释掉的（/* ... */），它展示了修改 AR6200.bcs 脚本文件以添加条件的传统方法。
// 作者选择不使用这种方法，而是使用 EXTEND_TOP。
/*COPY_EXISTING ~ar6200.bcs~ ~override~// 复制结局区域脚本
  DECOMPILE_BCS_TO_BAF// 将 BCS 编译成可编辑的 BAF 文本
  // 在 Mazzy 结局触发条件中添加一个限制：如果玩家选择了纯友谊路线，则不触发此结局。
    REPLACE_TEXTUALLY ~Global("MazzyBio","GLOBAL",0)~
            ~Global("MazzyBio","GLOBAL",0) !Global("YF_MazzyFriendship","GLOBAL",1)~
  COMPILE_BAF_TO_BCS// 将修改后的 BAF 重新编译成 BCS
BUT_ONLY_IF_IT_CHANGES// 仅在文件实际被修改时才覆盖*/

// 对 Anomen 结局也进行类似处理
COPY_EXISTING ~ar6200.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
  // 添加条件：如果玩家选择了邪恶 Anomen 或者 Clara 死亡，则不触发此结局。
    REPLACE_TEXTUALLY ~Global("AnomenBio","GLOBAL",0)~
            ~Global("AnomenBio","GLOBAL",0) !Global("YF_DarksideAnomen","GLOBAL",2) OR(2) !Global("YF_LordAndLadyDelryn","GLOBAL",5) Dead("YF_Clara")~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES

// --- 使用 EXTEND_TOP 修改结局脚本 ---
// 将自定义的结局控制逻辑脚本添加到 AR6200.bcs 的顶部，以确保它在原有结局逻辑之前执行。
// 这个脚本 (YF_AR6200.baf) 可能包含了防止冲突结局同时触发的逻辑。
EXTEND_TOP ~AR6200.bcs~ ~LivingClara/scripts/YF_AR6200.baf~

//Clara
// --- Clara 的结局 ---
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla01.2da~ //Basic Epilogue
REPLACE ~99999~ @47
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla02.2da~ //Marries Anomen, not dead
REPLACE ~99999~ @78
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla03.2da~ //Both Cor & Anomen dead
REPLACE ~99999~ @79
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla04.2da~ //Marries Cor, both alive
REPLACE ~99999~ @80
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla05.2da~ //Marries Cor, A dead
REPLACE ~99999~ @81
COPY ~LivingClara/epilogue/YF_Clara.2da~ ~override/YF_Cla06.2da~ //Marries Cor, C dead, A alive
REPLACE ~99999~ @82

//Anomen
// --- Anomen 的结局 ---
COPY ~LivingClara/epilogue/YF_Anom.2da~ ~override/YF_Ano01.2da~ //Married Clara, not dead
REPLACE ~99999~ @83
COPY ~LivingClara/epilogue/YF_Anom.2da~ ~override/YF_Ano02.2da~ //Darkside Romance Epilogue
REPLACE ~99999~ @98
COPY ~LivingClara/epilogue/YF_Anom.2da~ ~override/YF_Ano03.2da~ //Darkside Romance God Epilogue
REPLACE ~99999~ @99

//BCaesar and Ratatoskr's Personal Rest Check Files (For all of them mods)
PRINT @155 /*RestCheck Files: to help with all the joys of mod compatibility.*/
ACTION_IF NOT FILE_EXISTS_IN_GAME ~YF_rcstp.spl~ THEN BEGIN
COPY ~LivingClara/restcheck/YF_rcstp.spl~ ~override~
EXTEND_BOTTOM ~baldur.bcs~ ~LivingClara/restcheck/player1dreamsSoA.baf~
EXTEND_BOTTOM ~baldur25.bcs~ ~LivingClara/restcheck/player1dreamsToB.baf~
EXTEND_BOTTOM ~aerie.bcs~ ~LivingClara/restcheck/_rc_aerie.baf~
EXTEND_BOTTOM ~aeri25.bcs~ ~LivingClara/restcheck/_rc_aeri25.baf~
EXTEND_BOTTOM ~anomen.bcs~ ~LivingClara/restcheck/_rc_anomen.baf~
EXTEND_BOTTOM ~dorn.bcs~ ~LivingClara/restcheck/_rc_dorn.baf~
EXTEND_BOTTOM ~edwin.bcs~ ~LivingClara/restcheck/_rc_edwin.baf~
EXTEND_BOTTOM ~haerdali.bcs~ ~LivingClara/restcheck/_rc_haerdali.baf~
EXTEND_BOTTOM ~haer25.bcs~ ~LivingClara/restcheck/_rc_haer25.baf~
EXTEND_BOTTOM ~hexxat.bcs~ ~LivingClara/restcheck/_rc_hexxat.baf~
EXTEND_BOTTOM ~hexxa25.bcs~ ~LivingClara/restcheck/_rc_hexxa25.baf~
EXTEND_BOTTOM ~imoen2.bcs~ ~LivingClara/restcheck/_rc_imoen2.baf~
EXTEND_BOTTOM ~imoe25.bcs~ ~LivingClara/restcheck/_rc_imoe25.baf~
EXTEND_BOTTOM ~jaheira.bcs~ ~LivingClara/restcheck/_rc_jaheira.baf~
EXTEND_BOTTOM ~keldorn.bcs~ ~LivingClara/restcheck/_rc_keldorn.baf~
EXTEND_BOTTOM ~keld25.bcs~ ~LivingClara/restcheck/_rc_keld25.baf~
EXTEND_BOTTOM ~mazzy.bcs~ ~LivingClara/restcheck/_rc_mazzy.baf~
EXTEND_BOTTOM ~nalia.bcs~ ~LivingClara/restcheck/_rc_nalia.baf~
EXTEND_BOTTOM ~neer25.bcs~ ~LivingClara/restcheck/_rc_neera25.baf~
EXTEND_BOTTOM ~rasaad.bcs~ ~LivingClara/restcheck/_rc_rasaad.baf~
EXTEND_BOTTOM ~sarev25.bcs~ ~LivingClara/restcheck/_rc_sarev25.baf~
EXTEND_BOTTOM ~viconia.bcs~ ~LivingClara/restcheck/_rc_viconia.baf~
EXTEND_BOTTOM ~vico25.bcs~ ~LivingClara/restcheck/_rc_vico25.baf~
END

//Code to make Malchor Harpell not come back after you kill him.
// ----- 修正 Malchor Harpell 的行为 -----
// 修复一个潜在的 Bug：确保 Malchor Harpell 在被杀死后不会复活。
// 复制游戏主脚本文件 baldur.bcs 以便进行修改。
COPY_EXISTING ~baldur.bcs~ ~override~
    // 将 BCS 二进制脚本文件反编译成可编辑的 BAF 文本文件。
  DECOMPILE_BCS_TO_BAF   //This code changes the game file to something Weidu can edit
          // 在文本中查找并替换特定内容。
        // 这里查找条件 `!Exists("c6harp")` (Malchor Harpell 不存在)，
    REPLACE_TEXTUALLY //This code says to replace every instance of the first ~~ with what's inside the second ~~
	~!Exists("c6harp")~
	        // 并将其替换为 `!Exists("c6harp") !Dead("c6harp")` (Malchor Harpell 不存在 *或者* 已经死亡)。
        // 这样可以防止在 Malchor 被杀死后，相关脚本逻辑错误地认为他又“存在”了。
            ~!Exists("c6harp") !Dead("c6harp")~
			    // 将修改后的 BAF 文本文件重新编译成 BCS 二进制脚本文件。
  COMPILE_BAF_TO_BCS //This code then recompiles the file into a game file
      // 仅在文件实际被修改时才覆盖原文件，避免不必要的操作。
BUT_ONLY_IF_IT_CHANGES  //To stop this from happening endlessly

//Area script file
    // ----- 扩展结局区域脚本 -----
    // 扩展结局区域 AR6200 的脚本顶部，添加一个脚本，当激活 LivingClara 的结局时，关闭现有的结局。
EXTEND_TOP ~AR6200.bcs~ ~LivingClara/scripts/YF_AR6200X.baf~  //This script just turns off existing epilogues when ours are active.

//FtE Only portion
 // 结束对 FtE 部分的处理。

    // 复制 Clara 的 WAV 音频文件到 override 目录。
//COPY ~LivingClara/Clara/audio/wav_files~ ~override~  
    // 调用 Weidu 的内置函数 HANDLE_AUDIO 来处理音频文件（可能包括转换或复制 OGG 文件）。
LAF HANDLE_AUDIO
        // 传递给函数的字符串变量，指定音频文件的路径。
  STR_VAR
    audio_path = ~LivingClara/audio~// OGG 文件源路径
    oggdec_path = ~LivingClara/audio~ // OGG 解码器路径 (可能未使用)
    sox_path = ~LivingClara/audio~ // Sox 音频处理工具路径 (可能未使用)
END