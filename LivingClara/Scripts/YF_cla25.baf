// Clara's TOB scripts

// Clara should talk immediately when summoned.
IF
Global("YF_ClaraSummoned","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YF_ClaraSummoned","GLOBAL",2)
END

IF
Global("YF_ClaraSummoned","GLOBAL",2)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF	//Clara's script if Mazzy goes hostile.
	Global("YF_MazzyHostile","GLOBAL",4)
	InParty("YF_Clara") 
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
THEN
	RESPONSE #100
	Hide()
	MakeUnselectable(999999)
END 

IF	//Trigger for Clara leaving the party forever.
	Global("YF_ClaraLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//2nd Clara-Viconia Banter
IF
	InParty("Viconia") 
	InParty("YF_Clara")
	Global("YF_BCLARAVIC","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("YF_BCLARAVIC","GLOBAL",2)
	SetGlobalTimer("YF_CVPoison","GLOBAL",TWO_DAYS)
END

IF 	
	InParty("Viconia") 
	See("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("YF_Clara")
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARAVIC","GLOBAL",2)
	GlobalTimerExpired("YF_CVPoison","GLOBAL")
	OR(2)
	AreaCheck("AR5501")	      //Amkethran Inn
	AreaCheck("AR5003")       //Saradush Tavern - tankard tree
THEN
	RESPONSE #100
	SetGlobal("YF_BCLARAVIC","GLOBAL",3)
END

IF
	InParty("Viconia") 
	See("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("YF_Clara")
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARAVIC","GLOBAL",3)
THEN
	RESPONSE #100
	Interact("Viconia")
END

//Clara-Viconia on Raise
IF
	InParty("Viconia") 
	Detect("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("YF_Clara")
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARAVIC","GLOBAL",4)
THEN
	RESPONSE #100
	Interact("Viconia")
END


//Dorn Special Ability
IF
	Global("YF_BCLARADORN","GLOBAL",2)
	Global("YF_clarapw","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL423")
	SetGlobal("YF_clarapw","GLOBAL",1)
END	

//HaerDalis Special Ability
IF
	Global("YF_BCLARAHD","GLOBAL",2)
	Global("YF_claraSD","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_SDO")
	SetGlobal("YF_claraSD","GLOBAL",1)
END	

//Edwin Special Ability
IF
	Global("YF_BCLARAEDWIN","GLOBAL",2)
	Global("YF_claraEdSD","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_SDO")
	SetGlobal("YF_claraEdSD","GLOBAL",1)
END

//Valygar Special Ability
IF
	Global("YF_BCLARAVALYG","GLOBAL",3)
	Global("YF_ClaraVND","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_NOND")
	ActionOverride("Valygar",AddSpecialAbility("YF_NOND"))
	SetGlobal("YF_ClaraVND","GLOBAL",1)
END

//Keldorn Special Ability
IF
	Global("YF_BCLARAKELD","GLOBAL",2)
	Global("YF_ClaraKND","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_NOND")
	SetGlobal("YF_ClaraKND","GLOBAL",1)
END

//Viconia Special Ability
IF
	Global("YF_BCLARAVIC","GLOBAL",5)
	Global("YF_claracanpoison","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_PoisA")
	SetGlobal("YF_claracanpoison","GLOBAL",1)
END

//1st Minsc Ability
IF
	Global("YF_BCLARAMINSC","GLOBAL",2)
	Global("YF_ClaraSeesAll","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL732")
	ApplySpellRES("YF_TIRED","YF_Clara")
	SetGlobal("YF_ClaraSeesAll","GLOBAL",1)
END	

//2nd Minsc Ability
IF
	Global("YF_BCLARAMINSC","GLOBAL",3)
	Global("YF_ClaraBooTalk","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL311")
	SetGlobal("YF_ClaraBooTalk","GLOBAL",1)
END

//3rd Minsc Ability
IF
	Global("YF_BCLARAMINSC","GLOBAL",4)
	Global("YF_ClaraHulksOut","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL321")
	SetGlobal("YF_ClaraHulksOut","GLOBAL",1)
END

//1st Aerie Ability (Charm Animal)
IF
	Global("YF_BCLARAAERIE","GLOBAL",2)
	Global("YF_ClaraCFBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL311")
	SetGlobal("YF_ClaraCFBoon","GLOBAL",1)
END

//2nd Aerie (Find traps)
IF
	Global("YF_BCLARAAERIE","GLOBAL",3)
	Global("YF_ClaraAerieBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_FTRAP")
	SetGlobal("YF_ClaraAerieBoon","GLOBAL",1)
END

//Cernd (Find traps)
IF
	Global("YF_BCLARACERND","GLOBAL",2)
	Global("YF_ClaraCerndBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_FTRAP")
	SetGlobal("YF_ClaraCerndBoon","GLOBAL",1)
END

//Imoen - Special Trap
IF
	Global("YF_BCLARAIMOEN","GLOBAL",4)
	Global("YF_ClaraKaboom","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("YF_FIRE")
	ActionOverride("Imoen2",AddSpecialAbility("SPCL911"))
	SetGlobal("YF_ClaraKaboom","GLOBAL",1)
END


//Timer for Last Korgan banter
IF
	InParty("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARAKOR","GLOBAL",2)
THEN
	RESPONSE #100 
	SetGlobal("YF_BCLARAKOR","GLOBAL",3)
	SetGlobalTimer("YF_CKOrgyTimer","GLOBAL",SEVEN_DAYS)
END	
	
IF
	InParty("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARAKOR","GLOBAL",3)
	GlobalTimerExpired("YF_CKOrgyTimer","GLOBAL")
THEN
	RESPONSE #100 
	SetGlobal("YF_BCLARAKOR","GLOBAL",4)
END

IF
	InParty("Korgan") 
	See("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	!AreaType(DUNGEON) 
	Global("YF_BCLARAKOR","GLOBAL",4) 
	!AreaCheck("AR4500") 
	PartyRested()
THEN
	RESPONSE #100
	Interact("Korgan")
END

//Timer for Last Nalia Banter (middle trigger in d file)
IF
	InParty("Nalia")
	InParty("YF_Clara") 
	Global("YF_BCLARANALIA","GLOBAL",2)
THEN
	RESPONSE #100 
	SetGlobal("YF_BCLARANALIA","GLOBAL",3)
	SetGlobalTimer("YF_GirlsNightOut","GLOBAL",FOUR_DAYS)
END	

IF
	InParty("Nalia")
	InParty("YF_Clara")
	!StateCheck("Nalia",CD_STATE_NOTVALID) 
	!StateCheck("YF_Clara",CD_STATE_NOTVALID) 
	Global("YF_BCLARANALIA","GLOBAL",4)
THEN
	RESPONSE #100
	UndoExplore()
	Interact("Nalia")
END

//Give Clara wedding ring if chose marriage in Anomen talk
IF
	Global("YF_ClaraRingTOB","GLOBAL",1)
	InParty("YF_Clara")
THEN
	RESPONSE #100
	GiveItemCreate("YF_Rng45","YF_Clara",0,0,0)
	XEquipItem("YF_Rng45","YF_Clara",SLOT_RING_LEFT,EQUIP)
	SetGlobal("YF_ClaraRingTOB","GLOBAL",2)
END

//Clara wears sex change belt
IF
HasItemEquiped("YF_BELTC",Myself)
Gender(Myself,FEMALE)
Global("YF_ClaraGoesMale","GLOBAL",0)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,MALE)
Polymorph(THIEF_MALE_HUMAN)
SetGlobal("YF_ClaraGoesMale","GLOBAL",1)
END

IF
!HasItemEquiped("YF_BELTC",Myself)
Gender(Myself,MALE)
Global("YF_ClaraGoesMale","GLOBAL",1)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,FEMALE)
Polymorph(THIEF_FEMALE_HUMAN)
SetGlobal("YF_ClaraGoesMale","GLOBAL",0)
END


//Aerie Wing Response
IF
	Global("YF_AerieWingResponse","GLOBAL",1)
	InParty("YF_Clara")
	!StateCheck("YF_Clara",CD_STATE_NOTVALID)
	Global("YF_ClaraWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Timer for Second/6th Mazzy-Clara Talk
IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("YF_Clara") 
	OR(2)
	Global("YF_MazzyClara","GLOBAL",3)
	Global("YF_MazzyClara","GLOBAL",15)
THEN
	RESPONSE #100
	IncrementGlobal("YF_MazzyClara","GLOBAL",1)
	SetGlobalTimer("YF_MClaraTimer","GLOBAL",THREE_DAYS)
END

IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("YF_Clara") 
	GlobalTimerExpired("YF_MClaraTimer","GLOBAL")
	OR(2)
	Global("YF_MazzyClara","GLOBAL",4)
	Global("YF_MazzyClara","GLOBAL",16)
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("YF_MazzyClara","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("YF_Clara") 
	OR(2)
	Global("YF_MazzyClara","GLOBAL",5)
	Global("YF_MazzyClara","GLOBAL",17)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
