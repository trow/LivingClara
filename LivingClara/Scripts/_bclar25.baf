// Clara's TOB scripts

// Clara should talk immediately when summoned.
IF
Global("ClaraSummoned","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("ClaraSummoned","GLOBAL",2)
END

IF
Global("ClaraSummoned","GLOBAL",2)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF	//Clara's script if Mazzy goes hostile.
	Global("_bMazzyHostile","GLOBAL",4)
	InParty("_bClara") 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
THEN
	RESPONSE #100
	Hide()
	MakeUnselectable(999999)
END 

IF	//Trigger for Clara leaving the party forever.
	Global("_bClaraLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//2nd Clara-Viconia Banter
IF
	InParty("Viconia") 
	InParty("_bClara")
	Global("_BBCLARAVIC","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_BBCLARAVIC","GLOBAL",2)
	SetGlobalTimer("_bCVPoison","GLOBAL",TWO_DAYS)
END

IF 	
	InParty("Viconia") 
	See("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARAVIC","GLOBAL",2)
	GlobalTimerExpired("_bCVPoison","GLOBAL")
	OR(2)
	AreaCheck("AR5501")	      //Amkethran Inn
	AreaCheck("AR5003")       //Saradush Tavern - tankard tree
THEN
	RESPONSE #100
	SetGlobal("_BBCLARAVIC","GLOBAL",3)
END

IF
	InParty("Viconia") 
	See("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARAVIC","GLOBAL",3)
THEN
	RESPONSE #100
	Interact("Viconia")
END

//Clara-Viconia on Raise
IF
	InParty("Viconia") 
	Detect("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARAVIC","GLOBAL",4)
THEN
	RESPONSE #100
	Interact("Viconia")
END


//Dorn Special Ability
IF
	Global("_BBCLARADORN","GLOBAL",2)
	Global("_bclarapw","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL423")
	SetGlobal("_bclarapw","GLOBAL",1)
END	

//HaerDalis Special Ability
IF
	Global("_BBCLARAHD","GLOBAL",2)
	Global("_bclaraSD","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BSDO")
	SetGlobal("_bclaraSD","GLOBAL",1)
END	

//Edwin Special Ability
IF
	Global("_BBCLARAEDWIN","GLOBAL",2)
	Global("_bclaraEdSD","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BSDO")
	SetGlobal("_bclaraEdSD","GLOBAL",1)
END

//Valygar Special Ability
IF
	Global("_BBCLARAVALYG","GLOBAL",3)
	Global("_bClaraVND","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BNOND")
	ActionOverride("Valygar",AddSpecialAbility("_BNOND"))
	SetGlobal("_bClaraVND","GLOBAL",1)
END

//Keldorn Special Ability
IF
	Global("_BBCLARAKELD","GLOBAL",2)
	Global("_bClaraKND","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BNOND")
	SetGlobal("_bClaraKND","GLOBAL",1)
END

//Viconia Special Ability
IF
	Global("_BBCLARAVIC","GLOBAL",5)
	Global("_bclaracanpoison","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BPoisA")
	SetGlobal("_bclaracanpoison","GLOBAL",1)
END

//1st Minsc Ability
IF
	Global("_BBCLARAMINSC","GLOBAL",2)
	Global("_bClaraSeesAll","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL732")
	ApplySpellRES("_BTIRED","_bClara")
	SetGlobal("_bClaraSeesAll","GLOBAL",1)
END	

//2nd Minsc Ability
IF
	Global("_BBCLARAMINSC","GLOBAL",3)
	Global("_bClaraBooTalk","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL311")
	SetGlobal("_bClaraBooTalk","GLOBAL",1)
END

//3rd Minsc Ability
IF
	Global("_BBCLARAMINSC","GLOBAL",4)
	Global("_bClaraHulksOut","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL321")
	SetGlobal("_bClaraHulksOut","GLOBAL",1)
END

//1st Aerie Ability (Charm Animal)
IF
	Global("_BBCLARAAERIE","GLOBAL",2)
	Global("_bClaraCFBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("SPCL311")
	SetGlobal("_bClaraCFBoon","GLOBAL",1)
END

//2nd Aerie (Find traps)
IF
	Global("_BBCLARAAERIE","GLOBAL",3)
	Global("_bClaraAerieBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BFTRAP")
	SetGlobal("_bClaraAerieBoon","GLOBAL",1)
END

//Cernd (Find traps)
IF
	Global("_BBCLARACERND","GLOBAL",2)
	Global("_bClaraCerndBoon","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BFTRAP")
	SetGlobal("_bClaraCerndBoon","GLOBAL",1)
END

//Imoen - Special Trap
IF
	Global("_BBCLARAIMOEN","GLOBAL",4)
	Global("_bClaraKaboom","GLOBAL",0)
THEN
	RESPONSE #100
	AddSpecialAbility("_BFIRE")
	ActionOverride("Imoen2",AddSpecialAbility("SPCL911"))
	SetGlobal("_bClaraKaboom","GLOBAL",1)
END


//Timer for Last Korgan banter
IF
	InParty("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARAKOR","GLOBAL",2)
THEN
	RESPONSE #100 
	SetGlobal("_BBCLARAKOR","GLOBAL",3)
	SetGlobalTimer("_bCKOrgyTimer","GLOBAL",SEVEN_DAYS)
END	
	
IF
	InParty("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARAKOR","GLOBAL",3)
	GlobalTimerExpired("_bCKOrgyTimer","GLOBAL")
THEN
	RESPONSE #100 
	SetGlobal("_BBCLARAKOR","GLOBAL",4)
END

IF
	InParty("Korgan") 
	See("Korgan") 
	!StateCheck("Korgan",CD_STATE_NOTVALID) 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	!AreaType(DUNGEON) 
	Global("_BBCLARAKOR","GLOBAL",4) 
	!AreaCheck("AR4500") 
	PartyRested()
THEN
	RESPONSE #100
	Interact("Korgan")
END

//Timer for Last Nalia Banter (middle trigger in d file)
IF
	InParty("Nalia")
	InParty("_bClara") 
	Global("_BBCLARANALIA","GLOBAL",2)
THEN
	RESPONSE #100 
	SetGlobal("_BBCLARANALIA","GLOBAL",3)
	SetGlobalTimer("_bGirlsNightOut","GLOBAL",FOUR_DAYS)
END	

IF
	InParty("Nalia")
	InParty("_bClara")
	!StateCheck("Nalia",CD_STATE_NOTVALID) 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	Global("_BBCLARANALIA","GLOBAL",4)
THEN
	RESPONSE #100
	UndoExplore()
	Interact("Nalia")
END

//Give Clara wedding ring if chose marriage in Anomen talk
IF
	Global("_bClaraRingTOB","GLOBAL",1)
	InParty("_bClara")
THEN
	RESPONSE #100
	GiveItemCreate("_BRing45","_bClara",0,0,0)
	XEquipItem("_BRing45","_bClara",SLOT_RING_LEFT,EQUIP)
	SetGlobal("_bClaraRingTOB","GLOBAL",2)
END

//Clara wears sex change belt
IF
HasItemEquiped("_BBELTC",Myself)
Gender(Myself,FEMALE)
Global("_bClaraGoesMale","GLOBAL",0)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,MALE)
Polymorph(THIEF_MALE_HUMAN)
SetGlobal("_bClaraGoesMale","GLOBAL",1)
END

IF
!HasItemEquiped("_BBELTC",Myself)
Gender(Myself,MALE)
Global("_bClaraGoesMale","GLOBAL",1)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,FEMALE)
Polymorph(THIEF_FEMALE_HUMAN)
SetGlobal("_bClaraGoesMale","GLOBAL",0)
END


//Aerie Wing Response
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID)
	Global("_bClaraWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Timer for Second/6th Mazzy-Clara Talk
IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("_bClara") 
	OR(2)
	Global("_BMazzyClara","GLOBAL",3)
	Global("_BMazzyClara","GLOBAL",15)
THEN
	RESPONSE #100
	IncrementGlobal("_BMazzyClara","GLOBAL",1)
	SetGlobalTimer("_bMClaraTimer","GLOBAL",THREE_DAYS)
END

IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("_bClara") 
	GlobalTimerExpired("_bMClaraTimer","GLOBAL")
	OR(2)
	Global("_BMazzyClara","GLOBAL",4)
	Global("_BMazzyClara","GLOBAL",16)
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_BMazzyClara","GLOBAL",1)
END

IF
	IfValidForPartyDialog("Mazzy")
	IfValidForPartyDialog("_bClara") 
	OR(2)
	Global("_BMazzyClara","GLOBAL",5)
	Global("_BMazzyClara","GLOBAL",17)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
