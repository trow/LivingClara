//Might need to move initial teleport here

//To make sure he can't die too early
IF
	!HasItemEquiped("IMOENHP1",Myself)  
	GlobalGT("_bRoseLibrary","GLOBAL",5)
	GlobalLT("_bRoseLibrary","GLOBAL",12)
THEN
	RESPONSE #100
	CreateItem("IMOENHP1",0,0,0)   //Item must be created first
	XEquipItem("IMOENHP1",Myself,SLOT_BELT,EQUIP)  //Once equipped, hp can't drop below 1
END

//So Gereth talks when he sees you
IF
	Global("_bRoseLibrary","GLOBAL",9)
	OR(6)
		Detect(Player1)
		Detect(Player2)
		Detect(Player3)
		Detect(Player4)
		Detect(Player5)
		Detect(Player6)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//To start him talking when he's almost dead
IF
	HPLT(Myself,5)
	Global("_bRoseLibrary","GLOBAL",11)
THEN
	RESPONSE #100
	SetGlobal("_bRoseLibrary","GLOBAL",12)
END

IF
	Global("_bRoseLibrary","GLOBAL",12)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Destroys him after final talk, code to put him back in the Adventurer's Mart is in AR0702 script
IF
	Global("_bRoseLibrary","GLOBAL",13)
	AreaCheck("AR1700")
THEN
	RESPONSE #100
	ReallyForceSpell(Myself,DRYAD_TELEPORT)
	Wait(1)
	DestroySelf()
	SetGlobal("_bRoseLibrary","GLOBAL",14)
END

//MoveBetweenAreasEffect("AR0702","SPDIMNDR",[972.643],SE) - alternate effect. Used other option to avoid him being retriggered as enemy