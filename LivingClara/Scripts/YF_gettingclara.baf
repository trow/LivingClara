// 目的：当 Hexxat 死亡（或处于死亡状态）且玩家队伍中没有她，同时玩家还没有 "OHHBAG" 物品时，
// 创建一只鸟来承载脚本而不是把所有判断都嵌入全局脚本by shohy
IF	//Script to create the bag if Hexxat dies.
	// 使用 OR(2) 表示接下来的两个条件满足其一即可。
	OR(2)
		// 检查对象 "hexxat" 是否已死亡。
		Dead("hexxat")
		// 或者检查对象 "hexxat" 是否处于 STATE_DEAD 状态。
		StateCheck("hexxat",STATE_DEAD)
	// 检查玩家队伍是否*没有*拥有物品 "OHHBAG"。
//	!PartyHasItem("OHHBAG")	//这里避免检查物品，交给鸟by shohy
	// 检查全局变量 "YF_HexxatBag" 是否等于 0。
	// 这是一个去重标志，确保下面的逻辑只执行一次。
	Global("YF_HexxatBag","GLOBAL",0)
	// 检查 Hexxat 是否*不*在队伍中（即使已死亡也检查）。
	!InPartyAllowDead("hexxat")
	// 检查全局变量 "OHH_dragorespite" 是否*不*等于 3。
    // 这可能与任务的其他部分或条件有关。
	!Global("OHH_dragorespite","GLOBAL",3)
THEN
    // 如果以上所有条件都满足，则执行以下响应。
	RESPONSE #100
		CreateCreature("YF_Hdead",[407.1434],1)	//召唤一只鸟，承载脚本和对话by shohy
	// 给予hexxat物品 "OHHBAG"（一个包含棺材的袋子）。
	//GiveItemCreate("OHHBAG","hexxat",0,0,0)
	// 使用 ActionOverride 命令，强制 Hexxat 掉落她的所有物品（包括刚刚创建的袋子）。
	//ActionOverride("hexxat",DropInventory())
	// 将全局变量 "YF_HexxatBag" 设置为 1，表示袋子已生成。
	//SetGlobal("YF_HexxatBag","GLOBAL",1)
	// 设置 "OHH_dragorespite" 为 3，可能是任务状态标志。
		SetGlobal("OHH_dragorespite","GLOBAL",3)
	// 在玩家角色头上显示一条描述性文本，解释发生了什么。
    // @1 是字符串引用。
	//ActionOverride(Player1,DisplayStringNoNameHead(Player1,@1)) /*You defeat the vampire, but she transforms into mist and quickly disappears. All that remains where she stood is an old sack. Perhaps if you took it, you could hunt the creature down.*/
END

// 目的：修复一个潜在的 Bug，如果玩家在特定场景中杀死了假的 Hexxat ("OHHFAK")，
// 但真正的 Hexxat ("hexxat") 没有被杀死，则强制杀死假的 Hexxat。
IF	//Bug fix for killing Fake Hexxat if Hexxat doesn't in their cutscene.
    // 检查区域 "OH7000" 中的变量 "OHH_hexxat" 是否等于 3。
    // 这可能表示假 Hexxat 场景的特定状态。
	Global("OHH_hexxat","OH7000",3)
	// 检查假 Hexxat "OHHFAK" 是否*没有*死亡。
	!Dead("OHHFAK")
THEN
	RESPONSE #100
	// 强制杀死假 Hexxat "OHHFAK"。
	Kill("OHHFAK")
END

// 目的：允许玩家在特定条件下（Hexxat 不在队伍中）杀死她。
// 通常情况下，Hexxat 可能是无敌的。
IF	//Script to allow you to kill Hexxat if she's not in the party (normally she's invulnerable)
    // 检查 Hexxat 的生命值是否低于 2 点 (HPLT = HP Less Than)。
	HPLT("hexxat",2)
	// 检查 Hexxat 是否*不*在队伍中。
	!InPartyAllowDead("hexxat")
THEN
	RESPONSE #100
	// 强制杀死 Hexxat。
	Kill("hexxat")
END

// 目的：启动 Clara 的等待计时器。在她被创造出来后，她会等待一段时间再出现。
IF	//Script for Clara's waiting timer.
	// 检查全局变量 "YF_ClaraWaiting" 是否等于 1。
	Global("YF_ClaraWaiting","GLOBAL",1)
THEN
	RESPONSE #100
	// 使用 ActionOverride 和 MakeGlobalOverride 命令，将 Clara 的状态保存到全局游戏文件 (GAM) 中。
    // 这确保即使玩家离开当前区域，Clara 的状态也能被记住。
	ActionOverride("YF_Clara",MakeGlobalOverride())  //Should put Clara in the GAM file, overwriting previous entry if exists
	// 设置一个全局计时器 "YF_ClaraWaitingTimer"，时长为一天 (ONE_DAY)。
	SetGlobalTimer("YF_ClaraWaitingTimer","GLOBAL",ONE_DAY)
	// 将 "YF_ClaraWaiting" 设置为 2，表示计时器已启动。
	SetGlobal("YF_ClaraWaiting","GLOBAL",2)
END
// 目的：当 Clara 的等待计时器过期后，更新状态。
IF	
	// 检查 "YF_ClaraWaiting" 是否等于 2。
	Global("YF_ClaraWaiting","GLOBAL",2)
	// 检查全局计时器 "YF_ClaraWaitingTimer" 是否已过期。
	GlobalTimerExpired("YF_ClaraWaitingTimer","GLOBAL")
THEN
	RESPONSE #100
	// 将 "YF_ClaraWaiting" 设置为 3，表示等待时间结束，准备移动 Clara。
	SetGlobal("YF_ClaraWaiting","GLOBAL",3)
END

IF	
	// 目的：如果等待结束，且玩家已与 Cor Delryn 结婚（YF_LadyDelryn=1）或与 Anomen Delryn 结婚（YF_LordAndLadyDelryn=5），
	// 则将 Clara 移动到 Delryn 庄园 (AR1001)。
	Global("YF_ClaraWaiting","GLOBAL",3)
	// 使用 OR(2) 表示接下来的两个条件满足其一即可。
	OR(2)
	// 检查是否与 Cor 结婚。
	Global("YF_LadyDelryn","GLOBAL",1)   //Married Cor
	// 检查是否与 Anomen 结婚。
	Global("YF_LordAndLadyDelryn","GLOBAL",5)    //Married Anomen
THEN
	RESPONSE #100
	// 将 "YF_ClaraWaiting" 重置为 0。
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR1001" 的坐标 [652.209]。
	MoveGlobal("AR1001","YF_Clara",[652.209])  //Delryn Manor
	// 注释掉的 EscapeAreaMove 命令是另一种移动方式。
	//ActionOverride("YF_Clara",EscapeAreaMove("AR1001",652,209,SWW)) //Delryn Manor
END	

// 目的：如果等待结束，且 Anomen 恋爱线中有特定事件发生（YF_ClaraCC=1），但玩家未与 Delryn 家族成员结婚，
// 则将 Clara 移动到 Copper Coronet (AR0406)。
IF

	// 检查 "YF_ClaraWaiting" 是否等于 3。
	Global("YF_ClaraWaiting","GLOBAL",3)
	// 检查 "YF_ClaraCC" 是否等于 1（Anomen 恋爱线相关）。
	Global("YF_ClaraCC","GLOBAL",1)  //Sets during Anomen Romance
	// 检查未与 Cor 结婚。
	!Global("YF_LadyDelryn","GLOBAL",1)
	// 检查未与 Anomen 结婚。
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0406" 的坐标 [1478.1644]。
	MoveGlobal("AR0406","YF_Clara",[1478.1644])   //Copper Coronet
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0406",1478,1644,SE))  //Copper Coronet
END	

// 目的：如果等待结束，且玩家没有与 Delryn 家族成员结婚，也没有触发 Anomen 恋爱线事件，
// 并且影贼公会未敌对，Aran 未死，且玩家已与 Renal 交谈过（作为 Hexxat 或 Clara），
// 则将 Clara 移动到影贼总部 (AR0306)。
IF	
	Global("YF_ClaraWaiting","GLOBAL",3)
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
	!Global("YF_LadyDelryn","GLOBAL",1)
	!Global("YF_ClaraCC","GLOBAL",1)
	!Global("AttackedThieves","GLOBAL",1)  //Thieves Not Hostile
	!Dead("aran")
	OR(2) 
	Global("YF_RenalOHHFAK","GLOBAL",2) //Talked to Renal as Hexxat
	Global("YF_RenalClara","GLOBAL",2)   //Talked to Renal as Clara
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0306" 的坐标 [873.563]。
	MoveGlobal("AR0306","YF_Clara",[873.563])   //Shadow Thief Headquarters
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0306",873,563,SW)) //Shadow Thief Headquarters
END	

// 目的：如果等待结束，且玩家没有开启吟游诗人要塞 (PLAYHOUSE=0)，未与 Delryn 家族成员结婚，
// 也没有触发 Anomen 恋爱线事件，但影贼公会已敌对或 Aran 已死，
// 则将 Clara 移动到 Five Flagons (AR0509)。
IF	
	Global("YF_ClaraWaiting","GLOBAL",3)
	!Global("PLAYHOUSE","GLOBAL",1)  
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
	!Global("YF_LadyDelryn","GLOBAL",1)
	!Global("YF_ClaraCC","GLOBAL",1)
	OR(2)
	Global("AttackedThieves","GLOBAL",1)  //Thieves Hostile
	Dead("aran") //Aran Dead
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0509" 的坐标 [470.404]。
	MoveGlobal("AR0509","YF_Clara",[470.404]) //Five Flagons
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0509",470,404,E)) //Five Flagons
END	

// 目的：如果等待结束，且玩家已开启吟游诗人要塞 (PLAYHOUSE=1)，未与 Delryn 家族成员结婚，
// 也没有触发 Anomen 恋爱线事件，但影贼公会已敌对或 Aran 已死，
// 则将 Clara 移动到吟游诗人要塞区域 (AR0522) 的相同坐标。
IF	
	Global("YF_ClaraWaiting","GLOBAL",3)
	Global("PLAYHOUSE","GLOBAL",1)   //Bard Stronghold
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
	!Global("YF_LadyDelryn","GLOBAL",1)
	!Global("YF_ClaraCC","GLOBAL",1)
	OR(2)
	Global("AttackedThieves","GLOBAL",1)  //Thieves Hostile
	Dead("aran") //Aran Dead
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0522" 的坐标 [470.404]。
	MoveGlobal("AR0522","YF_Clara",[470.404]) //Five Flagons
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0522",470,404,E)) //Five Flagons
END	

// 目的：如果等待结束，且玩家没有开启吟游诗人要塞，未与 Delryn 家族成员结婚，
// 也没有触发 Anomen 恋爱线事件，影贼公会未敌对，Aran 未死，且未与 Renal 交谈过，
// 则将 Clara 移动到 Five Flagons (AR0509)。
IF	
	Global("YF_ClaraWaiting","GLOBAL",3)
	!Global("PLAYHOUSE","GLOBAL",1)  
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
	!Global("YF_LadyDelryn","GLOBAL",1)
	!Global("YF_ClaraCC","GLOBAL",1)
	!Global("AttackedThieves","GLOBAL",1)  //Thieves Not Hostile
	!Dead("aran") //Aran Not Dead
	!Global("YF_RenalOHHFAK","GLOBAL",2) //Talked to Renal as Hexxat
	!Global("YF_RenalClara","GLOBAL",2)   //Talked to Renal as Clara
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0509" 的坐标 [470.404]。
	MoveGlobal("AR0509","YF_Clara",[470.404]) //Five Flagons
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0509",470,404,E)) //Five Flagons
END	

// 目的：如果等待结束，且玩家已开启吟游诗人要塞，未与 Delryn 家族成员结婚，
// 也没有触发 Anomen 恋爱线事件，影贼公会未敌对，Aran 未死，且未与 Renal 交谈过，
// 则将 Clara 移动到吟游诗人要塞区域 (AR0522) 的相同坐标。
IF	
	Global("YF_ClaraWaiting","GLOBAL",3)
	Global("PLAYHOUSE","GLOBAL",1)   //Bard Stronghold
	!Global("YF_LordAndLadyDelryn","GLOBAL",5)
	!Global("YF_LadyDelryn","GLOBAL",1)
	!Global("YF_ClaraCC","GLOBAL",1)
	!Global("AttackedThieves","GLOBAL",1)  //Thieves Not Hostile
	!Dead("aran") //Aran Not Dead
	!Global("YF_RenalOHHFAK","GLOBAL",2) //Talked to Renal as Hexxat
	!Global("YF_RenalClara","GLOBAL",2)   //Talked to Renal as Clara
THEN
	RESPONSE #100
	SetGlobal("YF_ClaraWaiting","GLOBAL",0)
	// 将 Clara 移动到区域 "AR0522" 的坐标 [470.404]。
	MoveGlobal("AR0522","YF_Clara",[470.404]) //Five Flagons
	//ActionOverride("YF_Clara",EscapeAreaMove("AR0522",470,404,E)) //Five Flagons
END	





