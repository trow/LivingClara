//These scripts keep scenes that trigger on rest from triggering on the same rest as dreams in Throne of Bhaal. EXTEND_BOTTOM ~baldur25.bcs~

//Rest check timer.
IF
	Global("YF_restcheck","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",3)
	RealSetGlobalTimer("YF_restchecktimer1","GLOBAL",TWO_TURNS)
END

IF
	RealGlobalTimerExpired("YF_restchecktimer1","GLOBAL")
		Global("YF_restcheck","GLOBAL",3)
THEN
	RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",0)
END

//Restcheck back-up fix timer.
IF
	Global("YF_restcheck","GLOBAL",1)
	Global("YF_restcheckfix","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobalTimer("YF_restchecktimerfix","GLOBAL",ONE_DAY)
	SetGlobal("YF_restcheckfix","GLOBAL",1)
END

IF
	GlobalTimerExpired("YF_restchecktimerfix","GLOBAL")
	Global("YF_restcheckfix","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",0)
	SetGlobal("YF_restcheckfix","GLOBAL",0)
END


IF // Yoshimo Grave Dream (Yoshimo Romance Mod)
	Global("YoshimoRomanceActive","GLOBAL",2)
	AreaCheck("AR4500")
	Global("YoshimoGraveDream","GLOBAL",0)
	CombatCounter(0)
	Global("YF_restcheck","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",1)
END

IF
	Global("YoshimoGraveDream","GLOBAL",1)
	Global("YF_YoshimoGraveDream","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",2)
	SetGlobal("YF_YoshimoGraveDream","GLOBAL",1)
END


IF	//Yoshimo Candle Dream (Yoshimo Romance Mod)
	GlobalTimerExpired("YoshimoToBNextDream","GLOBAL")
	Global("YoshimoRomanceActive","GLOBAL",2)
	AreaCheck("AR4500")
	Global("YoshimoCandleDream","GLOBAL",0)
	CombatCounter(0)
	Global("YF_restcheck","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",1)
END

IF
	Global("YoshimoCandleDream","GLOBAL",1)
	Global("YF_YoshimoCandleDream","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("YF_restcheck","GLOBAL",2)
	SetGlobal("YF_YoshimoCandleDream","GLOBAL",1)
END
